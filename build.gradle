plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.hidetake.ssh' version '2.10.1'
    id 'java'
}

group = 'org.chaos'
version = '2.0'
sourceCompatibility = '15'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

tasks.withType(Test).all {
    jvmArgs += '--enable-preview'
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

repositories {
    mavenCentral()
}

dependencies {
    //dev
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.discord4j:discord4j-core:3.1.1'

    //test
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'io.projectreactor:reactor-test'

    //build
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    //run
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java:5.1.6'

}

test {
    useJUnitPlatform()
}


remotes {
    webServer {
        host = project.properties["ssh-host"]
        user = 'adventskalender'
        identity = file(project.properties["ssh-private-key"])
    }
}

task deploy {
    dependsOn bootJar
    doLast {
        ssh.run {
            session(remotes.webServer) {
                execute 'systemctl --user stop adventskalender'
                put from: "${projectDir}/build/libs/advenskalender-${version}.jar", into: '/home/adventskalender/app/advenskalender.jar'
                execute 'systemctl --user start adventskalender'
            }
        }
    }
}